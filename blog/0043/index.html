<!DOCTYPE html>

<html lang="en">

  <head>

    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">

    <!-- METADATA -->
    <title>Representations of execution traces — The trek goes on (Josh Ko’s blog)</title>
    <meta name="description" content="There are different ways of representing relations as datatypes.
I wonder whether we can consciously exploit the connection and systematically derive intrinsic datatypes for algorithm design.
As an experiment, I revisit the maximum prefix sum problem.
The outcome is inconclusive though.">
    <!-- END OF METADATA -->

    <link rel="apple-touch-icon" sizes="180x180" href="../../images/apple-touch-icon.png">
    <link rel="icon" type="image/png" sizes="32x32" href="../../images/favicon-32x32.png">
    <link rel="icon" type="image/png" sizes="16x16" href="../../images/favicon-16x16.png">
    <link rel="manifest" href="../../images/site.webmanifest">
    <link rel="mask-icon" href="../../images/safari-pinned-tab.svg" color="#002147">
    <link rel="shortcut icon" href="../../images/favicon.ico">
    <meta name="msapplication-TileColor" content="#002147">
    <meta name="msapplication-config" content="../../images/browserconfig.xml">

    <!-- Latest compiled and minified CSS -->
    <link rel="stylesheet" href="https://maxcdn.bootstrapcdn.com/bootstrap/3.3.7/css/bootstrap.min.css" integrity="sha384-BVYiiSIFeK1dGmJRAkycuHAHRg32OmUcww7on3RYdg4Va+PmSTsz/K68vbdEjh4u" crossorigin="anonymous">

    <!-- Optional theme -->
    <link rel="stylesheet" href="https://maxcdn.bootstrapcdn.com/bootstrap/3.3.7/css/bootstrap-theme.min.css" integrity="sha384-rHyoN1iRsVXV4nD0JutlnGaslCJuC7uwjduW9SVrLvRYooPp2bWYgmgJQIXwl/Sp" crossorigin="anonymous">

    <!-- jQuery -->
    <script src="https://ajax.googleapis.com/ajax/libs/jquery/1.12.4/jquery.min.js"></script>

    <!-- Latest compiled and minified JavaScript -->
    <script src="https://maxcdn.bootstrapcdn.com/bootstrap/3.3.7/js/bootstrap.min.js" integrity="sha384-Tc5IQib027qvyjSMfHjOMaLkfuWVxZxUPnCJA7l2mCWNIpG9mGCD8wGNIcPD7Txa" crossorigin="anonymous"></script>

    <link rel="preconnect" href="https://fonts.googleapis.com">
    <link rel="preconnect" href="https://fonts.gstatic.com" crossorigin>
    <link href="https://fonts.googleapis.com/css2?family=Fira+Code&family=Noto+Sans+TC:wght@400;700&family=Open+Sans:ital,wght@0,400;0,600;1,400&display=swap" rel="stylesheet">

    <script src="../../mathjax-config.js" defer></script>
    <script id="MathJax-script" defer src="https://cdn.jsdelivr.net/npm/mathjax@3/es5/tex-chtml.js"></script>

    <link rel="stylesheet" href="../../style.css">

  </head>

  <body>

    <div><div class="container main blog">

      <div class="row blog-header">
        <div class="col-sm-12">
          <h1>The trek goes on</h1>
          <div class="subtitle"><a href="/">Josh Ko</a>’s <a href="/blog/">blog</a></div>
        </div>
      </div>

<!-- POST -->
<div class="post-number">
0043
</div>
<h2>Representations of execution traces</h2>
<div class="post-time">Posted at 16:47 on 14 January 2025, and revised at 11:23 on 7 March 2025</div>
<p>So far the clearest general principle arising from the <a href="/#publication-8798a704">binomial tabulation story</a> is the interpretation of inhabitants of indexed datatypes as execution traces of nondeterministic functions  — that is, relations.
It’s been well known that inductively defined relations —or logic programs— can be expressed as indexed datatypes.
For example, we can define the relation that maps a list to one of its prefixes as a datatype</p>
<pre><code>data Init {A : Set} : List A → List A → Set where
  nil       :              Init []       []
  cons-nil  :              Init (x ∷ xs) []
  cons-cons : Init xs ys → Init (x ∷ xs) (x ∷ ys)
</code></pre>
<p>which directly corresponds to a logic program, with the three constructors corresponding to the clauses of the logic program.
The logic program is executed by (implicitly) constructing derivations using the clauses, and such derivations can be thought of as execution traces.</p>
<p>But there is more than one way to represent execution traces.
Another way is to introduce a continuation and allow execution traces to be concatenated:</p>
<pre><code>data InitC {A : Set} (P : List A → Set) : List A → Set where
  nil       : P []                  → InitC P []
  cons-nil  : P []                  → InitC P (x ∷ xs)
  cons-cons : InitC (P ∘ (x ∷_)) xs → InitC P (x ∷ xs)
</code></pre>
<p>For <code>Init</code>, an execution trace ends with <code>nil</code> or <code>cons-nil</code>, whereas for <code>InitC</code>, a <code>nil</code> or <code>cons-nil</code> constructor contains a further execution trace specified by the continuation <code>P</code>.
For example, we can similarly define the suffix relation as</p>
<pre><code>data TailC {A : Set} (P : List A → Set) : List A → Set where
  nil         : P []       → TailC P []
  cons-whole  : P (x ∷ xs) → TailC P (x ∷ xs)
  cons-proper : TailC P xs → TailC P (x ∷ xs)
</code></pre>
<p>and then define the segment relation by function composition (rather than relation composition!):</p>
<pre><code>SegC : {A : Set} (P : List A → Set) → List A → Set
SegC = InitC ∘ TailC
</code></pre>
<p>I’m still baffled about whether <code>P</code> can legitimately be called a continuation (or more generally, what this construction is mathematically), although the idea seems to be essentially the same as continuation-passing style: an ordinary function computes a result and ends its execution, whereas its continuation-passing version passes the result to a continuation, and therefore specifies only an initial portion of its execution, with the rest of the execution abstracted as the continuation.</p>
<p>Logically, <code>InitC</code> existentially quantifies over execution traces and results — <code>InitC P xs</code> is isomorphic to <code>Σ[ ys ∈ List A ] Init xs ys × P ys</code>.
However, compared to the latter representation as a tuple, <code>InitC</code> is actually less convenient to use because the result <code>ys</code> and the proof of type <code>P ys</code> are hidden at the end of the execution trace and cannot be accessed directly.
Moreover, if we want to perform induction on <code>InitC</code>, we can do it on <code>Init</code> as well.
So <code>InitC</code> doesn’t seem to have any advantage over the tuple representation, and for convenience we redefine it as a record:</p>
<pre><code>record InitC {A : Set} (P : List A → Set) (xs : List A) : Set where
  constructor initC
  field
    {prefix} : List A
    isPrefix : Init xs prefix
    payload  : P prefix
</code></pre>
<p>Things seem to become more interesting if we consider an inductive representation of <em>universal</em> quantification over execution traces and results: <code>(ys : List A) → Init xs ys → P ys</code>.
Instead of representing just one execution trace, we can represent all possible execution traces as a tree, which branches whenever there’s a nondeterministic choice.
For <code>Init</code>, such trees can be defined as</p>
<pre><code>data Inits {A : Set} (P : List A → Set) : List A → Set where
  [_] : P []                         → Inits P []
  _∷_ : P [] → Inits (P ∘ (x ∷_)) xs → Inits P (x ∷ xs)
</code></pre>
<p>This representation is first-order, so it’s at least markedly different from the higher-order/functional representation and worth considering.
The binomial tabulation story shows that this is the design principle underlying Richard Bird’s use of binary/binomial trees.
The principle seems general enough, and there should be other examples.</p>
<p>A natural place to find more examples is optimisation problems, which involve universal quantification over candidate solutions.
For example, in the maximum prefix sum problem, we can use <code>Inits</code> to represent all the prefix sums of an input list (from which we’ll choose a maximum later): defining</p>
<pre><code>record Exactly {A : Set} (x : A) : Set where
  constructor exactly
  field
    payload      : A
    {{equality}} : payload ≡ x
</code></pre>
<p>to attach an equality constraint to a value, we can write a function <code>inits</code> to compute all the prefixes</p>
<pre><code>inits : (xs : List A) → Inits Exactly xs
inits []       = [ exactly [] ]
inits (x ∷ xs) = exactly [] ∷ mapInits (mapExactly (x ∷_)) (inits xs)
</code></pre>
<p>and then, assuming a type <code>Val : Set</code> that supports addition, compose <code>inits</code> with ‘<code>map sum</code>’ to compute all the prefix sums:</p>
<pre><code>prefixSums : (xs : List Val) → Inits (Exactly ∘ sum) xs
prefixSums = mapInits (mapExactly sum) ∘ inits
</code></pre>
<p>Alternatively, we can directly write</p>
<pre><code>prefixSums' : (xs : List Val) → Inits (Exactly ∘ sum) xs
prefixSums' []       = [ exactly zero ]
prefixSums' (x ∷ xs) = exactly zero
                     ∷ mapInits (mapExactly (x +_)) (prefixSums' xs)
</code></pre>
<p>which can be seen to be the result of fusing ‘<code>map sum</code>’ into <code>inits</code>.
The development is minimal but looks nice so far:
The specification is encoded as a precise type that nevertheless can have different implementations, so the type isn’t over-specifying too much.
There’s not much overhead to write the precisely typed programs (compared to writing simply typed ones).
What I find particularly interesting is that the correctness of <code>prefixSums'</code> used to be established with respect to <code>prefixSums</code> by fold fusion in a simply typed setting, whereas here the correctness is established solely by typing.
This means that we can write <code>prefixSums'</code> directly and still make sure it’s correct without going through a derivation, which can be tedious especially when the program to write is obvious.
On the other hand, program derivation techniques —in particular more powerful theorems like those dealing with optimisation problems in the later sections of the Algebra of Programming book— will still be useful when the program to write is not obvious.
The point, as always, is to be able to skip boring steps and concentrate on where ingenuity is required.</p>
<p>One can argue that the type used above</p>
<pre><code>IndPrefixSums : Set
IndPrefixSums = (xs : List Val) → Inits (Exactly ∘ sum) xs
</code></pre>
<p>is still over-specifying because it requires the prefix sums to be returned in a non-empty list and in a specific order, and that a more adequate specification is</p>
<pre><code>PrefixSums : Set
PrefixSums = (xs ys : List Val) → Init xs ys → Exactly (sum ys)
</code></pre>
<p>because it only requires that we can get every prefix sum through a functional interface without saying anything about the internals of the function.
That is, it makes more sense to start from <code>PrefixSums</code> as the specification, and then make a choice to switch to <code>IndPrefixSums</code> to use a non-empty list to represent all the prefix sums, formally by applying</p>
<pre><code>fromInits : Inits P xs → (ys : List A) → Init xs ys → P ys
fromInits [ p ]    _  nil          = p
fromInits (p ∷ ps) _  cons-nil     = p
fromInits (p ∷ ps) _ (cons-cons i) = fromInits ps _ i
</code></pre>
<p>which fits the inductive representation into the functional interface, and can be thought of as the soundness of <code>Inits</code> (correctly implementing the universal quantification).
A persisting problem in traditional functional program derivation is that functional specifications can seriously affect the result of derivations and need to be chosen carefully.
I think this shows that such ‘specifications’ are not starting points but actually intermediate stops, to arrive at which some reasoning steps need to have already been taken.
The Algebra of Programming book proposed to start from relations as specifications and then refine them to functions as implementations, but the relation calculus is too complicated and difficult to master.
(Even <a href="https://scm.iis.sinica.edu.tw/home/">Shin</a>, who has worked with the relation calculus extensively, admits that he has to look up definitions and laws frequently.)
By contrast, dependent types work nicely as specifications because they include a straightforward logical language, and it’s easier to refine them to more detailed types (by writing and applying <code>fromInits</code>, for example) and eventually to functions thanks to the Curry-Howard intuition.
One could argue that relational definitions could still be expanded and reasoned about set-/type-theoretically, but the results of expansion tend to be obscure and tedious to manipulate.
I think it should be the other way around: we work in the Curry–Howard way by default, and introduce relational abstractions where suitable.</p>
<p>Unfortunately, the ‘maximum’ part in maximum prefix sum seems to get messier.
My idea is that maximum can be implemented for non-empty lists whose elements have a decidable total ordering; then, <code>Inits</code> is an ornamentation of non-empty lists, so we should be able to just lift maximum for non-empty lists to work on <code>Inits</code>.
Sadly, such lifting is still not available more than a decade after the invention of ornaments, so for now we have to implement maximum for <code>Inits</code> manually.
The element types in <code>Inits</code> are from a type family, and the decidable total ordering needs to make sense between possibly different types from the same family:</p>
<pre><code>record MaxFam {A : Set} (P : A → Set) : Set₁ where
  constructor maxFam
  field
    _≤_     : ∀ {x y} → P x → P y → Set
    ≤-refl  : ∀ {x} {p : P x} → p ≤ p
    ≤-trans : ∀ {x y z} {p : P x} {q : P y} {r : P z}
            → p ≤ q → q ≤ r → p ≤ r
    ≤-dec   : ∀ {x y} (p : P x) (q : P y) → p ≤ q ⊎ q ≤ p
</code></pre>
<p>My implementation of maximum for <code>Inits</code> isn’t particularly pleasing:</p>
<pre><code>maxInits : {P : List A → Set} (mf : MaxFam P) → let open MaxFam mf in
           Inits P xs → Σ[ (initC _ q) ∈ InitC P xs ]
                        Inits (λ ys → Σ[ p ∈ P ys ] p ≤ q) xs
maxInits mf [ p ]    = initC nil p , [ (p , mf .MaxFam.≤-refl) ]
maxInits mf (p ∷ ps) with maxInits (MaxFam-fun (_ ∷_) mf) ps
maxInits mf (p ∷ ps) | initC i q , ps' with mf .MaxFam.≤-dec p q
maxInits mf (p ∷ ps) | initC i q , ps' | inj₁ p≤q =
  initC (cons-cons i) q , (p , p≤q) ∷ ps'
maxInits mf (p ∷ ps) | initC i q , ps' | inj₂ q≤p =
  initC cons-nil p , (p , mf .MaxFam.≤-refl) ∷
                     mapInits (map₂ (flip (mf .MaxFam.≤-trans) q≤p)) ps'
</code></pre>
<p>Usually, the type of maximum should be a dependent function type saying that the output is an element of the input list that’s greater than all the elements of the list.
This could be stated by instantiating the generic <code>All</code> construction applied to <code>Inits</code>, but I fear that stacking constraints on a datatype would make it too complicated.
In this case, if <code>P</code> has been made precise enough such that we only need to care about the existence of inhabitants and not about which particular inhabitants are exhibited, then we can simply produce another <code>Inits</code> that only imposes more constraints on its elements and doesn’t insist that the elements are the input ones.
For example, for maximum prefix sum <code>P</code> is <code>Exactly ∘ sum</code>, and the family of types are uniquely inhabited (and thus extremely precise).
Through Curry–Howard, ‘imposing more constraints’ means pairing with proofs, and I like how <code>maxInits</code> returns the input list with elements augmented with proofs as we know more about the ordering among those elements — that is, knowledge is reified as proofs.
What I don’t like is that the Agda definition looks messy somehow, and in particular it looks inconvenient for equational reasoning.
I’m not sure exactly what’s wrong though.</p>
<p>Back to maximum prefix sum, for which we can give a specification</p>
<pre><code>MaxPrefixSum : Set
MaxPrefixSum = (xs : List Val)
             → Σ[ m ∈ Val ] Σ[ ys ∈ List Val ]
               Init xs ys × m ≡ sum ys ×
               ((zs : List Val) → Init xs zs → sum zs ≤ m)
</code></pre>
<p>and refine it to a type with <code>InitC</code> and <code>Inits</code>:</p>
<pre><code>IndMaxPrefixSum : Set
IndMaxPrefixSum =
    (xs : List Val)
  → Σ[ (initC _ (exactly m)) ∈ InitC (Exactly ∘ sum) xs ]
    Inits (λ ys → Σ[ (exactly n) ∈ Exactly (sum ys) ] n ≤ m) xs
</code></pre>
<p>This type can again be implemented by either a composition of existing functions or a direct definition:</p>
<pre><code>maxPrefixSum : IndMaxPrefixSum
maxPrefixSum = maxInits (MaxFam-fun sum (MaxFam-Exactly MaxFam-Val))
             ∘ mapInits (mapExactly sum) ∘ inits

maxPrefixSum' : IndMaxPrefixSum
maxPrefixSum' [] = initC nil (exactly zero) , [ (exactly zero , ≤-refl) ]
maxPrefixSum' (x ∷ xs) with maxPrefixSum' xs
maxPrefixSum' (x ∷ xs) | initC i (exactly m) , ps with ≤-dec (x + m) zero
maxPrefixSum' (x ∷ xs) | initC i em , ps | inj₁ x+m≤0 =
  initC cons-nil (exactly zero) ,
  (exactly zero , ≤-refl) ∷ mapInits (map-Σ (mapExactly (x +_)) (flip ≤-trans x+m≤0 ∘ +-mono-r x)) ps
maxPrefixSum' (x ∷ xs) | initC i em , ps | inj₂ 0≤x+m =
  initC (cons-cons i) (mapExactly (x +_) em) ,
  (exactly zero , 0≤x+m) ∷ mapInits (map-Σ (mapExactly (x +_)) (+-mono-r x)) ps
</code></pre>
<p>The direct definition <code>maxPrefixSum'</code> looks even messier.
I can still roughly see and imagine that this is the result of fusing <code>maxInits</code> with <code>prefixSums'</code>, but I can’t imagine actually doing the fusion.
Another messy definition is the soundness of <code>IndMaxPrefixSum</code>:</p>
<pre><code>IndMaxPrefixSum-soundness : IndMaxPrefixSum → MaxPrefixSum
IndMaxPrefixSum-soundness f xs =
  let (initC {ys} i (exactly m {{meq}}) , ps) = f xs
  in  m , ys , i , meq , λ zs j → see-below zs (fromInits ps zs j)
  where
    see-below : {m : Val} (zs : List Val)
              → Σ[ (exactly n) ∈ Exactly (sum zs) ] n ≤ m → sum zs ≤ m
    see-below _ (exactly _ {{refl}} , ineq) = ineq
</code></pre>
<p>This might be argued to be either a success or a failure of the Curry–Howard paradigm — you don’t have to think much to prove this, but the only interesting bit, which is the use of <code>fromInits</code>, is buried deep in boring details for restructuring the proofs.
There doesn’t seem to be a fundamentally better way though — if we were working in relation calculus, we would use point-free combinators to do something similar, which I think is just harder.
Perhaps the best way to think about this is that this kind of proof is simply trivial and should be automatically synthesised and then ignored by the programmer (who only verifies it intuitively).</p>
<p>An important question that should be asked is whether it’s beneficial to introduce <code>Inits</code> into this development — what if we directly implement <code>MaxPrefixSum</code> rather than refine it to <code>IndMaxPrefixSum</code>?</p>
<pre><code>maxPrefixSum'' : MaxPrefixSum
maxPrefixSum'' [] = zero , _ , nil , refl , λ { _ nil → ≤-refl }
maxPrefixSum'' (x ∷ xs) with maxPrefixSum'' xs
maxPrefixSum'' (x ∷ xs) | m , _ , i , meq , f with ≤-dec (x + m) zero
maxPrefixSum'' (x ∷ xs) | m , _ , i , meq , f | inj₁ x+m≤0 =
  zero , _ , cons-nil , refl ,
  λ { _ cons-nil → ≤-refl; _ (cons-cons j) → ≤-trans (+-mono-r x (f _ j)) x+m≤0 }
maxPrefixSum'' (x ∷ xs) | m , ys , i , meq , f | inj₂ 0≤x+m =
  (x + m) , _ , cons-cons i , cong (x +_) meq ,
  λ { _ cons-nil → 0≤x+m; _ (cons-cons j) → +-mono-r x (f _ j) }
</code></pre>
<p>This doesn’t look bad — in particular we get to omit all the <code>map</code>s.
Shin suspects that the functional representation would be inefficient in practice though.
Theoretically, I suspect that the functional representation doesn’t really support induction, particularly after a discussion with <a href="https://yangzhixuan.github.io">Zhixuan</a>.
It should be worthwhile to do a more systematic comparison, but not in this post.</p>
<p>One last thing that I should mention is that this isn’t actually the kind of development I wanted to try in the first place.
I was hoping that by relating datatypes to relations, I could do some form of ‘datatype derivation’ by importing relational derivation techniques, but I end up discussing fusion still at the program level.
That is, what I really want is a ‘datatype calculus’ that helps us to design highly customised indexed datatypes for intrinsically typed programming.
Maximum prefix sum doesn’t seem to be a good example because the result is just a value rather than an inhabitant of a datatype into which we can embed constraints.
(On the other hand, if we also take into account the output proof that the result is maximum, that can still be expressed as a datatype.
But somehow I end up just reusing <code>Inits</code>…)
I reckon I’m still very far away from my goal.</p>
<p><strong>Agda file:</strong> <a href="MPS.agda">MPS.agda</a></p>

<!-- END OF POST -->

      <div class="footer-stamp"><img id="stamp" src="../../images/PiSigma.svg" alt="End of page"></div>

    </div></div>

  </body>

</html>
